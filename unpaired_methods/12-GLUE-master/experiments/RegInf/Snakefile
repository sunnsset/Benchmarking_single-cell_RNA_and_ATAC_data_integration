rule all:
    input:
        "s03_peak_gene_validation",
        "s05_evaluate_gene_tf",
        "s06_genomic_tracks",
        "s07_violin",
        "s09_recovery"

rule s01_preprocessing:
    input:
        "../../data/dataset/10x-Multiome-Pbmc10k-RNA.h5ad",
        "../../data/dataset/10x-Multiome-Pbmc10k-ATAC.h5ad",
        "../../data/hic/Javierre-2016/Human_hg38/Digest_Human_HindIII.rmap",
        "../../data/hic/Javierre-2016/Human_hg38/Digest_Human_HindIII_baits_e75_ID.baitmap",
        "../../data/hic/Javierre-2016/PCHiC_peak_matrix_cutoff5.tsv",
        "../../data/eqtl/GTEx-v8/bed/Whole_Blood.v8.signif_variant_gene_pairs.bed.gz",
        "../../data/eqtl/GTEx-v8/GTEx_Analysis_v8_eQTL/Whole_Blood.v8.egenes.txt.gz",
        ipynb="s01_preprocessing.run.ipynb"
    output:
        directory("s01_preprocessing"),
        html="s01_preprocessing.html"
    log:
        "s01_preprocessing_nbconvert.log"
    threads: 1
    shell:
        "jupyter nbconvert --to html "
        "--ExecutePreprocessor.timeout=3600000 "
        "--execute {input.ipynb} --output {output.html} > {log} 2>&1"

rule s02_glue:
    input:
        "s01_preprocessing",
        lambda wildcards: "s08_corrupt" if "corrupted" in wildcards.prior else [],
        ipynb="s02_glue.run.ipynb"
    output:
        directory("s02_glue/prior:{prior}/seed:{seed}"),
        html="s02_glue_prior:{prior}_seed:{seed}.html"
    log:
        "s02_glue_prior:{prior}_seed:{seed}_nbconvert.log"
    threads: 4
    resources: gpu=1
    shell:
        "PRIOR={wildcards.prior} SEED={wildcards.seed} jupyter nbconvert --to html "
        "--ExecutePreprocessor.timeout=3600000 "
        "--execute {input.ipynb} --output {output.html} > {log} 2>&1"

rule s03_peak_gene_validation:
    input:
        "s01_preprocessing",
        "e01_cicero",
        expand("s02_glue/prior:d/seed:{i}", i=range(4)),
        ipynb="s03_peak_gene_validation.run.ipynb"
    output:
        directory("s03_peak_gene_validation"),
        html="s03_peak_gene_validation.html"
    log:
        "s03_peak_gene_validation_nbconvert.log"
    threads: 1
    shell:
        "jupyter nbconvert --to html "
        "--ExecutePreprocessor.timeout=3600000 "
        "--execute {input.ipynb} --output {output.html} > {log} 2>&1"

rule s04_infer_gene_tf:
    input:
        "s01_preprocessing",
        "../../data/chip/ENCODE/TF-human/combined-GRCh38.bed.gz",
        expand("s02_glue/prior:dcq/seed:{i}", i=range(4)),
        ipynb="s04_infer_gene_tf.run.ipynb"
    output:
        directory("s04_infer_gene_tf"),
        html="s04_infer_gene_tf.html"
    log:
        "s04_infer_gene_tf_nbconvert.log"
    threads: 20
    shell:
        "jupyter nbconvert --to html "
        "--ExecutePreprocessor.timeout=3600000 "
        "--execute {input.ipynb} --output {output.html} > {log} 2>&1"

rule s05_evaluate_gene_tf:
    input:
        "s01_preprocessing",
        "s04_infer_gene_tf",
        "../../data/database/TRRUST-v2/trrust_rawdata.human.tsv",
        ipynb="s05_evaluate_gene_tf.run.ipynb"
    output:
        directory("s05_evaluate_gene_tf"),
        html="s05_evaluate_gene_tf.html"
    log:
        "s05_evaluate_gene_tf_nbconvert.log"
    threads: 1
    shell:
        "jupyter nbconvert --to html "
        "--ExecutePreprocessor.timeout=3600000 "
        "--execute {input.ipynb} --output {output.html} > {log} 2>&1"

rule s06_genomic_tracks:
    input:
        "s01_preprocessing",
        "s04_infer_gene_tf",
        ipynb="s06_genomic_tracks.run.ipynb"
    output:
        directory("s06_genomic_tracks"),
        html="s06_genomic_tracks.html"
    log:
        "s06_genomic_tracks_nbconvert.log"
    threads: 1
    shell:
        "jupyter nbconvert --to html "
        "--ExecutePreprocessor.timeout=3600000 "
        "--execute {input.ipynb} --output {output.html} > {log} 2>&1"

rule s07_violin:
    input:
        "s01_preprocessing",
        ipynb="s07_violin.run.ipynb"
    output:
        directory("s07_violin"),
        html="s07_violin.html"
    log:
        "s07_violin_nbconvert.log"
    threads: 1
    shell:
        "jupyter nbconvert --to html "
        "--ExecutePreprocessor.timeout=3600000 "
        "--execute {input.ipynb} --output {output.html} > {log} 2>&1"

rule s08_corrupt:
    input:
        "s01_preprocessing",
        ipynb="s08_corrupt.run.ipynb"
    output:
        directory("s08_corrupt"),
        html="s08_corrupt.html"
    log:
        "s08_corrupt_nbconvert.log"
    threads: 1
    shell:
        "jupyter nbconvert --to html "
        "--ExecutePreprocessor.timeout=3600000 "
        "--execute {input.ipynb} --output {output.html} > {log} 2>&1"

rule s09_recovery:
    input:
        "s08_corrupt",
        expand("s02_glue/prior:dcq/seed:{i}", i=range(4)),
        expand("s02_glue/prior:hard_corrupted_dcq/seed:{i}", i=range(4)),
        expand("s02_glue/prior:soft_corrupted_dcq/seed:{i}", i=range(4)),
        ipynb="s09_recovery.run.ipynb"
    output:
        directory("s09_recovery"),
        html="s09_recovery.html"
    log:
        "s09_recovery_nbconvert.log"
    threads: 1
    shell:
        "jupyter nbconvert --to html "
        "--ExecutePreprocessor.timeout=3600000 "
        "--execute {input.ipynb} --output {output.html} > {log} 2>&1"

rule e01_cicero:
    input:
        "s01_preprocessing",
        ipynb="e01_cicero.run.ipynb"
    output:
        directory("e01_cicero"),
        html="e01_cicero.html"
    log:
        "e01_cicero_nbconvert.log"
    threads: 1
    shell:
        "jupyter nbconvert --to html "
        "--ExecutePreprocessor.timeout=3600000 "
        "--execute {input.ipynb} --output {output.html} > {log} 2>&1"

rule jupytext:
    input:
        lambda wildcards: "{notebook}.R" if wildcards.notebook in (
            "e01_cicero",
        ) else "{notebook}.py"
    output:
        "{notebook}.run.ipynb"
    log:
        "{notebook}_jupytext.log"
    threads: 1
    shell:
        "jupytext --to notebook -o {output} {input} > {log} 2>&1"
